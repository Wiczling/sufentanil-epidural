$PROBLEM sufentanil_epidural

$INPUT ID TIME AMT RATE DV CMT MDV EVID SEX AGE WT HEIGHT TINFS TINFR DOSES DOSER OUT NUM

$DATA ../../../../data/derived/nonmem_data_v02.csv  
IGNORE=I
IGNORE=(OUT.EQ.1)
IGNORE=(CMT.EQ.22)
IGNORE=(CMT.EQ.33)

$SUBROUTINES ADVAN13 TOL=6

$MODEL 
  COMP  = (ABS)
  COMP  = (CENTRAL)
  COMP  = (PERIPHERAL1)
  COMP  = (PERIPHERAL2)

$PK
;PK PARAMETERS
 CL = THETA(1)*EXP(ETA(1))
 VC = THETA(2)*EXP(ETA(2))
 Q1  = THETA(3)*EXP(ETA(3))
 VP1 = THETA(4)*EXP(ETA(4))
 Q2  = THETA(5)*EXP(ETA(5))
 VP2 = THETA(6)*EXP(ETA(6))
 KA = THETA(7)*EXP(ETA(7))

;SECONDARY PARAMETERS 
 K20 = CL/VC;
 K23 = Q1/VC;
 K32 = Q1/VP1;
 K24 = Q2/VC;
 K42 = Q2/VP2;
 
$DES
 DADT(1) = -KA*A(1)
 DADT(2) =  KA*A(1)-K23*A(2)+K32*A(3)-K24*A(2)+K42*A(4)-K20*A(2)
 DADT(3) =          K23*A(2)-K32*A(3)
 DADT(4) =          K24*A(2)-K42*A(4)
 
$ERROR

IPRED = A(2)/VC*1000 ;ug/L->ng/L=pg/ml
Y=IPRED*(1+EPS(2))+EPS(1)  


$THETA  
; https://pubmed.ncbi.nlm.nih.gov/8533912/
; VC = 14.6, VP1 = 66, VP2 = 608, Cl = 52.8 L/h, and Q = 102 L/h i 40.8 L/h
52.8 FIX    ;[L/h] CL
14.6 FIX    ;[L] VC
102 FIX    ;[L/h] Q1
66 FIX     ;[L] VP1
40.8 FIX    ;[L/h] Q2
608 FIX     ;[L] VP2
(0,1)       ;[1/h] KA

$OMEGA     
0.1   ;[P] ETA-CL
0 FIX ;[P] ETA-VC
0 FIX ;[P] ETA-Q1
0 FIX ;[P] ETA-VP1
0 FIX ;[P] ETA-Q2
0 FIX ;[P] ETA-VP2
0 FIX  ;[P] ETA-K41

$SIGMA
(0.01 FIX)  ;[P] ADD pg/ml
(0.4)       ;[P] PROP

$EST MAXEVAL=9999 METHOD=1 INTER SIGL=6 NSIG=2 PRINT=1 MSFO=./1.msf
$COV PRINT=E MATRIX=R

$TABLE NUM ID TIME EVID MDV CMT PRED IPRED CWRES WRES NPDE NOPRINT NOAPPEND ONEHEADERALL
FILE=sdtab1.tab
$TABLE NUM ID ETAS(1:LAST) KA CL VC Q1 VP1 Q2 VP2 NOPRINT NOAPPEND ONEHEADERALL
FILE=patab1.tab