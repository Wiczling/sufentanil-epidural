$PROBLEM sufentanil_epidural

$INPUT ID TIME AMT RATE DV CMT MDV EVID SEX AGE WT HEIGHT TINFS TINFR DOSES DOSER OUT NUM

$DATA ../../../../data/derived/nonmem_data_v02.csv  
IGNORE=I
IGNORE=(OUT.EQ.1)
IGNORE=(CMT.EQ.22)
IGNORE=(CMT.EQ.33)

$SUBROUTINES ADVAN13 TOL=6

$MODEL 
  COMP  = (ABS)
  COMP  = (CENTRAL)
  COMP  = (PERIPHERAL)

$PK
;PK PARAMETERS
 CL = THETA(1)*EXP(ETA(1))
 V1 = THETA(2)*EXP(ETA(2))
 Q  = THETA(3)*EXP(ETA(3))
 V2 = THETA(4)*EXP(ETA(4))
 KA = THETA(5)*EXP(ETA(5))

;SECONDARY PARAMETERS 
 K20 = CL/V1;
 K23 = Q/V1;
 K32 = Q/V2;
 
$DES
 DADT(1) = -KA*A(1)
 DADT(2) =  KA*A(1)-K23*A(2)+K32*A(3)-K20*A(2)
 DADT(3) =          K23*A(2)-K32*A(3)
 
$ERROR

IPRED = A(2)/V1*1000 ;ug/L->ng/L=pg/ml
Y=IPRED*(1+EPS(2))+EPS(1)  


$THETA  

(0,45.3)      ;[L/h] CL/F
(0,7.90)      ;[L] V1/F
(0,38.3)      ;[L/h] Q/F
(0,481)       ;[L] V2/F
(0,0.1)     ;[1/h] KA

$OMEGA     
0.1  ;[P] ETA-CL
0 FIX  ;[P] ETA-V1
0 FIX  ;[P] ETA-Q
0 FIX  ;[P] ETA-V2
0 FIX  ;[P] ETA-KA

$SIGMA
(0.01 FIX)  ;[P] ADD pg/ml
(0.4)       ;[P] PROP

$EST MAXEVAL=9999 METHOD=1 INTER SIGL=6 NSIG=2 PRINT=1 MSFO=./1.msf
$COV PRINT=E MATRIX=R

$TABLE NUM ID TIME EVID MDV CMT PRED IPRED CWRES WRES NPDE NOPRINT NOAPPEND ONEHEADERALL
FILE=sdtab1.tab
$TABLE NUM ID ETAS(1:LAST) KA CL V1 Q V2 NOPRINT NOAPPEND ONEHEADERALL
FILE=patab1.tab